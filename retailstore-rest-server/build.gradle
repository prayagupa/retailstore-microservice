
plugins {
    id 'application'

    /**
     * Get recent version from
     * https://plugins.gradle.org/plugin/org.springframework.boot
     */
    id 'org.springframework.boot' version '3.3.5'
    id 'name.remal.sonarlint' version '1.5.0'
}

repositories {
    mavenCentral()
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

group = 'com.prayagupd.api'
description = 'retailstore'

mainClassName = 'com.api.MicroserviceApplication'

dependencies {
    implementation project(':retailstore-rest-schema')

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    implementation group: 'io.github.resilience4j', name: 'resilience4j-spring-boot3', version: '2.2.0'
    implementation group: 'com.zaxxer', name: 'HikariCP'

    implementation group: 'mysql', name: 'mysql-connector-java'

    // 2.12.3 does not have @JsonIncludeProperties
    implementation (group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8') {
        exclude (group: "junit")
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'

    implementation group: 'io.projectreactor.netty', name: 'reactor-netty'

//    compile 'io.micrometer:micrometer-registry-atlas:latest.release'
    implementation group: 'io.micrometer', name: 'micrometer-core'
    implementation group: 'io.micrometer', name: 'micrometer-registry-prometheus'
//    implementation group: 'io.micrometer', name: 'micrometer-registry-graphite', version: micrometerVersion
//    implementation group: 'io.micrometer', name: 'micrometer-registry-statsd', version: micrometerVersion

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
        exclude module: "spring-boot-starter-logging"
        exclude module: "logback-classic"
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'

    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-actuator') {
        exclude module: "spring-boot-starter-logging"
        exclude module: "logback-classic"
    }

    /**
     * test deps
     */
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher'

//    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    testImplementation (group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude module: "logback-classic"
        exclude module: "spring-boot-starter-logging"
    }
    testImplementation group: 'org.springframework.security', name: 'spring-security-test'
    testImplementation group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.13.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
}

test {
    useJUnitPlatform()

    testLogging.showStandardStreams = true

    jacoco {
        excludes += []
    }

    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed"
    }

    test.finalizedBy(jacocoTestReport)
    test.finalizedBy(jacocoTestCoverageVerification)

    dependsOn cleanTest
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}
